set recursive-define ON
set quit-on-fail OFF

load defined numbers.vars

echo numbers loaded

 load stack noun-list0.fst
 define stem0 ;
 load stack noun-list1.fst
 define stem1 ;
 load stack noun-list2.fst
 define stem2 ;
 load stack noun-list3.fst
 define stem3 ;
 load stack noun-list4.fst
 define stem4 ;
 load stack noun-list5.fst
 define stem5 ;
 load stack noun-list6.fst
 define stem6 ;
 load stack noun-list7.fst
 define stem7 ;
# load stack noun-productive.fst
# define stemProd ;
 load stack masdar-list.fst
 define masdarStem ;
 load stack past-part-list.fst
 define pastPartStem ;
 load stack present-part-list.fst
 define presentPartStem ;
 load stack future-part-list.fst
 define futurePartStem ;
 load stack negative-part-list.fst
 define negativePartStem ;
 load stack geonames-list.fst
 define geonamesStem ;
# include vich.txt, izacia.txt, acia.txt, ireba.txt
 load stack extracted-noun-adj.fst
 define extracted ;
 read regex   stem0 | stem1 | stem2 | stem3 | stem4 | stem5 | stem6 | stem7 # | stemProd
      	    | geonamesStem | extracted
      	    | masdarStem | pastPartStem | presentPartStem | futurePartStem | negativePartStem ;

define NounStem ;

# irregular forms that cannot easily be covered by rules
define NounStemIrr	{ამბ[ავ]·ი}:{ამბვ}  "@U.Cat.n@"  "@U.StemType.c@"  "@U.SyncStem.+@"  "@U.Style.subnorm@" ;

## not used? but see noun-transducer.lisp
#define DerivStem	[ NounStem .o. ["#" -> 0] ].l
#       			| ["0"|1|2|3|4|5|6|7|8|9]+ "-" ;

# Derivational affixes:

# -ული
# -ელი
# -ისმაგვარი
# -ისებრი
# -ისეული
# -ისფერი
# -ისნაირი
# -ობრივი, -ებრივი;  extracted form GRC, but only Nom forms
# -ამდელი; still missing

source obriv.regex

define NounGenStem NounStem
       		   "@U.Cat.n@"
       		   "@C.SubCat@" 
       		   [ "ი"
		     [ "@U.SyncStem.+@" "@U.RigidStem.-@"
		       [ "@U.StemType.c@" | "@U.StemType.ა@" | "@U.StemType.ე@" | "@U.StemType.ო@" ] ]
       		   | [ "@U.SyncStem.-@" "@U.RigidStem.+@"
		       [ "@U.StemType.v@" | "@U.StemType.ა@" ] ] ] 
		   ;

read regex NounGenStem ;

eliminate flag Cat
# eliminate flag StemType
eliminate flag RigidStem
eliminate flag SyncStem
eliminate flag StemType

define NounGenStemBare ;

define NounGenDerStem   [ NounGenStemBare ].l
              		[ {სმაგვარ·ი}:{სმაგვარ} "@U.Deriv.მაგვარ@"
              		| {სებრ·ი}:{სებრ} "@U.Deriv.ებრ@"
              		| {სეულ·ი}:{სეულ} "@U.Deriv.ეულ@"
              		| {სფ[ე]რ·ი}:{სფერ} "@U.Deriv.ფერ@" #k "@U.SyncStem.-@"
			| {სფ[ე]რ·ი}:{სფრ} "@U.Deriv.ფერ@" "@U.SyncStem.+@"
			| {სნაირ·ი}:{სნაირ} "@U.Deriv.ნაირ@" ]
		   	[ "@U.Cat.a@" ]
		   	"@U.StemType.c@" ;

define NounDerAdv	NounGenStemBare.l {საებრ} "@U.Cat.adv@" "@U.AdvType.mann@" "@U.Deriv.ებრ@" ;


# at least 3 chars
define NounUlStem	[ NounStem .o. [ ? ? ? ?* ] ]
       			"@U.Cat.n@" "@D.SubCat@" "@U.SyncStem.+@" ;

read regex NounUlStem ;

eliminate flag Cat
# eliminate flag StemType
eliminate flag RigidStem
eliminate flag SyncStem
eliminate flag StemType

define NounUlStemBare ;

# but: ლაბირინთული! fixme! or allow both
define NounUlStem  [ [ NounUlStemBare .o. [\რ]* ].l {ურ·ი}:{ურ}
       		   | [ NounUlStemBare .o. [?* რ ?*] ].l {ულ·ი}:{ულ} ]
		   "@U.Cat.a@" "@U.StemType.c@" "@U.Deriv.ურ@" ;

# -ელი

define NounElStem	[ NounStem .o. [ ? ? ? ?* ] ]
       			 "@U.Cat.prop@" [ "@U.SubCat.geo+area@" | "@U.SubCat.geo+city@" ] "@U.SyncStem.+@" ;

read regex NounElStem ;

eliminate flag Cat
eliminate flag SubCat
eliminate flag RigidStem
eliminate flag SyncStem
eliminate flag StemType

define NounElStemBare ;

define NounElStem  [ ~[?* {ეთ·ი} ?*] .o. NounElStemBare .o. [ {ა} -> 0 || _ .#.]].l {ელ·ი}:{ელ}
		   [ "@U.Cat.a@" "@U.SubCat.anim@" | "@U.Cat.n@" "@U.SubCat.anim+qual@" ]
		   "@U.StemType.c@" "@U.Deriv.ელ@" ;

# -ისმაგვარი
# -ისებრი
# -ისეული
# -ისფერი
# -ისნაირი

define NounDerStemBare [ NounGenStemBare .o. [ {ი} -> 0 || _ .#.] ].l ;

define AdjIan NounDerStemBare
	      {იან}
	      {·ი}:0
 	      "@U.Cat.a@"
 	      "@U.StemType.c@"
	      "@U.Deriv.იან@" ;

define AdjSaO {სა} [ NounDerStemBare .o. ~[ {სა} ?+ {ო} ] ] {ო} {·ჲ}:0
 	      "@U.Cat.a@"
 	      "@U.StemType.v@"
	      "@U.Deriv.სა-ო@" ;

define AdjUO  {უ} [ NounDerStemBare .o. ~[ {უ} ?+ {ო} ] ] {ო} {·ჲ}:0
 	      "@U.Cat.a@"
 	      "@U.StemType.v@"
	      "@U.Deriv.უ-ო@" ;

# expand!
define NumAdj [ ["0"|1|2|3|4|5|6|7|8|9]+
       	      	( [","|"-" ] ["0"|1|2|3|4|5|6|7|8|9]+ )
       	        "-"
       	      	| card001x999alph ( "-" card001x999alph ) ("-") ] # removed "@U.SubCat.range@"
       	      [ [ ( NounDerStemBare | "%" ) {იან}
		| {მხრივ} ]
	       	{·ი}:0
 	       	"@U.Cat.a@"
 	       	"@U.StemType.c@"
	      | [{კაცა}|{ფეხა}|{ნიშნა}] # add more!
	       	"@U.Cat.a@"
 	       	"@U.StemType.ა@"
	      ] ;

define OrdinalDigitsStem  (0:{მე-})
       			  ["0"|1|2|3|4|5|6|7|8|9]+
			  [ "-" [1|2|3|4|5|6|7|8|9] ["0"|1|2|3|4|5|6|7|8|9]* "@U.SubCat.range@"]*
       			  [ 0:"-" "@U.StemType.ე@"   "@U.SyncStem.+@"
			  | 0:{-ე} "@U.StemType.ე@"   "@U.SyncStem.-@" ]
			  "@U.Cat.num@" # is not converted to +Num, used to exclude N, Pron etc.,
		          "+Num":0 "+Ord":0 "+Digits":0 ;

# used in *noun-fst*
define Stem [ [ [ NounStem
       	      	| NounUlStem
	      	| NounElStem
	      	| NounGenDerStem
	      	| NounDerAdv
	      	| NGObriv
	      	| AdjIan
		| AdjSaO
		| AdjUO
	      	| NounStemIrr ]
		.o. [ "-" (->) 0 ] ] # allow hyphenless variant of hyphenated words, but not for Range
	      	| NumAdj
	      | OrdinalDigitsStem
	      | [ Cardinal | Ordinal | Fraction | Range | Distr ] "@U.Cat.num@" ] ;

# still missing: '-Taganze' etc. ('XvenTaganze')

define AugmentFlagDiacritics [
  "@D.DDCase@"
  [ "@R.DCat.n@" "@U.DDCat.n@" | "@D.DCat.n@" ]
  [ "@R.DCat.a@" "@U.DDCat.a@" | "@D.DCat.a@" ]
  [ "@R.DCat.num@" "@U.DDCat.num@" | "@D.DCat.num@" ]
  [ "@R.DCat.v-part@" "@U.DDCat.v-part@" | "@D.DCat.v-part@" ]
  [ "@R.DCat.masdar@" "@U.DDCat.masdar@" | "@D.DCat.masdar@" ]
  [ "@R.DCat.pron@" "@U.DDCat.pron@" | "@D.DCat.pron@" ]
  [ "@R.DCat.prop@" "@U.DDCat.prop@" | "@D.DCat.prop@" ]
  [ "@R.DCat.allq@" "@U.DDCat.allq@" | "@D.DCat.allq@" ]
   #  [ "@R.DSubCat.anim@" "@U.DDSubCat.prop@" | "@D.DCat.prop@" ] # ??

  [ "@R.DSubCat.anim+coll@" "@U.DDSubCat.anim+coll@" | "@D.DSubCat.anim+coll@" ]
  [ "@R.DSubCat.sup@" "@U.DDSubCat.sup@" | "@D.DSubCat.sup@" ]
  [ "@R.DSubCat.prop@" "@U.DDSubCat.prop@" | "@D.DSubCat.prop@" ]
  [ "@R.DSubCat.int@" "@U.DDSubCat.int@" | "@D.DSubCat.int@" ]
  [ "@R.DSubCat.rel@" "@U.DDSubCat.rel@" | "@D.DSubCat.rel@" ]
  [ "@R.DSubCat.poss@" "@U.DDSubCat.poss@" | "@D.DSubCat.poss@" ]
  [ "@R.DSubCat.name+firstname@" "@U.DDSubCat.name+firstname@" | "@D.DSubCat.name+firstname@" ]
  [ "@R.DSubCat.name+lastname@" "@U.DDSubCat.name+lastname@" | "@D.DSubCat.name+lastname@" ]
  [ "@R.DSubCat.name@" "@U.DDSubCat.name@" | "@D.DSubCat.name@" ]
  [ "@R.DSubCat.title@" "@U.DDSubCat.title@" | "@D.DSubCat.title@" ]
  [ "@R.DSubCat.org@" "@U.DDSubCat.org@" | "@D.DSubCat.org@" ]
  [ "@R.DSubCat.geo+area@" "@U.DDSubCat.geo+area@" | "@D.DSubCat.geo+area@" ]
  [ "@R.DSubCat.geo+city@" "@U.DDSubCat.geo+city@" | "@D.DSubCat.geo+city@" ]
  [ "@R.DSubCat.geo+river@" "@U.DDSubCat.geo+river@" | "@D.DSubCat.geo+river@" ]
  [ "@R.DSubCat.geo+sea@" "@U.DDSubCat.geo+sea@" | "@D.DSubCat.geo+sea@" ]
  [ "@R.DSubCat.masdar@" "@U.DDSubCat.masdar@" | "@D.DSubCat.masdar@" ]
  [ "@R.DSubCat.past-part@" "@U.DDSubCat.past-part@" | "@D.DSubCat.past-part@" ]
  [ "@R.DSubCat.present-part@" "@U.DDSubCat.present-part@" | "@D.DSubCat.present-part@" ]
  [ "@R.DSubCat.future-part@" "@U.DDSubCat.future-part@" | "@D.DSubCat.future-part@" ]
  [ "@R.DSubCat.negative-part@" "@U.DDSubCat.negative-part@" | "@D.DSubCat.negative-part@" ]
  [ "@R.DSubCat.mass@" "@U.DDSubCat.mass@" | "@D.DSubCat.mass@" ]
  [ "@R.DSubCat.animal@" "@U.DDSubCat.animal@" | "@D.DSubCat.animal@" ]
  [ "@R.DSubCat.temp@" "@U.DDSubCat.temp@" | "@D.DSubCat.temp@" ]
  [ "@R.DSubCat.anim@" "@U.DDSubCat.anim@" | "@D.DSubCat.anim@" ]
  [ "@R.DSubCat.anim+qual@" "@U.DDSubCat.anim+qual@" | "@D.DSubCat.anim+qual@" ]

  [ "@R.DCase.gen@" "@U.DDCase.gen@" | "@D.DCase.gen@" ]
  [ "@R.DCase.nom@" "@U.DDCase.nom@" | "@D.DCase.nom@" ]
  [ "@R.DLong.+@" "@U.DDL.+@" | "@D.DLong.+@" ]
  [ "@R.DNum.sg@" "@U.DDNum.sg@" | "@D.DNum.sg@" ]
  [ "@R.DNum.pl@" "@U.DDNum.pl@" | "@D.DNum.pl@" ]
  [ "@R.DOldPl.+@" "@U.DDOldPl.+@" | "@D.DOldPl.+@" ]

  "@C.DCat@" "@C.DSubCat@" "@C.DNum@" "@C.DOldPl@" "@C.DCase@"

  [ "@R.Cat.n@" "@U.DCat.n@" | "@D.Cat.n@" ]
  [ "@R.Cat.a@" "@U.DCat.a@" | "@D.Cat.a@" ]
  [ "@R.Cat.num@" "@U.DCat.num@" | "@D.Cat.num@" ]
  [ "@R.Cat.v-part@" "@U.DCat.v-part@" | "@D.Cat.v-part@" ]
  [ "@R.Cat.masdar@" "@U.DCat.masdar@" | "@D.Cat.masdar@" ]
  [ "@R.Cat.pron@" "@U.DCat.pron@" | "@D.Cat.pron@" ]
  [ "@R.Cat.prop@" "@U.DCat.prop@" | "@D.Cat.prop@" ]
  [ "@R.Cat.allq@" "@U.DCat.allq@" | "@D.Cat.allq@" ]
  
  [ "@R.SubCat.anim+coll@" "@U.DSubCat.anim+coll@" | "@D.SubCat.anim+coll@" ]
  [ "@R.SubCat.prop@" "@U.DSubCat.prop@" | "@D.SubCat.prop@" ]
  [ "@R.SubCat.sup@" "@U.DSubCat.sup@" | "@D.SubCat.sup@" ]
  [ "@R.SubCat.int@" "@U.DSubCat.int@" | "@D.SubCat.int@" ]
  [ "@R.SubCat.rel@" "@U.DSubCat.rel@" | "@D.SubCat.rel@" ]
  [ "@R.SubCat.poss@" "@U.DSubCat.poss@" | "@D.SubCat.poss@" ]
  [ "@R.SubCat.name+firstname@" "@U.DSubCat.name+firstname@" | "@D.SubCat.name+firstname@" ]
  [ "@R.SubCat.name+lastname@" "@U.DSubCat.name+lastname@" | "@D.SubCat.name+lastname@" ]
  [ "@R.SubCat.name@" "@U.DSubCat.name@" | "@D.SubCat.name@" ]
  [ "@R.SubCat.title@" "@U.DSubCat.title@" | "@D.SubCat.title@" ]
  [ "@R.SubCat.org@" "@U.DSubCat.org@" | "@D.SubCat.org@" ]
  [ "@R.SubCat.geo+area@" "@U.DSubCat.geo+area@" | "@D.SubCat.geo+area@" ]
  [ "@R.SubCat.geo+city@" "@U.DSubCat.geo+city@" | "@D.SubCat.geo+city@" ]
  [ "@R.SubCat.geo+river@" "@U.DSubCat.geo+river@" | "@D.SubCat.geo+river@" ]
  [ "@R.SubCat.geo+sea@" "@U.DSubCat.geo+sea@" | "@D.SubCat.geo+sea@" ]
  [ "@R.SubCat.masdar@" "@U.DSubCat.masdar@" | "@D.SubCat.masdar@" ]
  [ "@R.SubCat.past-part@" "@U.DSubCat.past-part@" | "@D.SubCat.past-part@" ]
  [ "@R.SubCat.present-part@" "@U.DSubCat.present-part@" | "@D.SubCat.present-part@" ]
  [ "@R.SubCat.future-part@" "@U.DSubCat.future-part@" | "@D.SubCat.future-part@" ]
  [ "@R.SubCat.negative-part@" "@U.DSubCat.negative-part@" | "@D.SubCat.negative-part@" ]
  [ "@R.SubCat.mass@" "@U.DSubCat.mass@" | "@D.SubCat.mass@" ]
  [ "@R.SubCat.animal@" "@U.DSubCat.animal@" | "@D.SubCat.animal@" ]
  [ "@R.SubCat.temp@" "@U.DSubCat.temp@" | "@D.SubCat.temp@" ]
  [ "@R.SubCat.anim@" "@U.DSubCat.anim@" | "@D.SubCat.anim@" ]
  [ "@R.SubCat.anim+qual@" "@U.DSubCat.anim+qual@" | "@D.SubCat.anim+qual@" ]

  [ "@R.Case.gen@" "@U.DCase.gen@" | "@D.Case.gen@" ]
  [ "@R.Long.+@" "@U.DLong.+@" | "@D.Long.+@" ]
  [ "@R.Num.sg@" "@U.DNum.sg@" | "@D.Num.sg@" ]
  [ "@R.Num.pl@" "@U.DNum.pl@" | "@D.Num.pl@" ]
  [ "@R.OldPl.+@" "@U.DOldPl.+@" | "@D.OldPl.+@" ]

  # Reset flag diacritics
 "@C.PlStem@" "@C.SyncStem@" "@C.Num@" "@C.OldPl@" "@C.CaseType@" "@C.StemType@" "@C.RigidStem@"
 "@C.Case@" "@C.Pers@" "@C.Form@" "@C.Cat@" "@C.SubCat@" # "@U.Cat.n@"
 "@C.Long@" # "@C.Lex@"
  ]
 ;

define ResolveCatFlagDiacritics  [
  [ "@R.DDCase@" #"@U.Lang.og@" # "@R.DDCase.gen@" "@R.Pp.გან@" "@R.DDOldPl.+@"
  [ [ "@R.DDCat.n@" "+N":0 | "@D.DDCat.n@" ]
    [ "@R.DDCat.a@" "+A":0 | "@D.DDCat.a@" ]
    [ "@R.DDCat.quant@" ["+A" "+Quant"]:0 | "@D.DDCat.quant@" ]
    [ "@R.DDCat.v-part@" "+VPart":0 | "@D.DDCat.v-part@" ]
    [ "@R.DDCat.masdar@" "+VN":0 | "@D.DDCat.masdar@" ]
    [ "@R.DDCat.prop@" "+N":0 "+Prop":0 | "@D.DDCat.prop@" ]
    [ "@R.DDCat.allq@" ["+A" "+Quant"]:0 | "@D.DDCat.allq@" ]
  
    [ "@R.DDSubCat.anim+coll@" ["+Hum""+Coll"]:0 | "@D.DDSubCat.anim+coll@" ]
    [ "@R.DDSubCat.prop@" "+N":0 "+Prop":0 | "@D.DDSubCat.prop@" ]
    [ "@R.DDSubCat.elat@" "+Elat":0 | "@D.DDSubCat.elat@" ]
    [ "@R.DDSubCat.int@" "+Int":0 | "@D.DDSubCat.int@" ]
    [ "@R.DDSubCat.rel@" "+Rel":0 | "@D.DDSubCat.rel@" ]
    [ "@R.DDSubCat.poss@" "+Poss":0 | "@D.DDSubCat.poss@" ]
    [ "@R.DDSubCat.name+firstname@" ["+Anthr" "+FirstName"]:0 | "@D.DDSubCat.name+firstname@" ]
    [ "@R.DDSubCat.name+lastname@" ["+Anthr" "+LastName"]:0 | "@D.DDSubCat.name+lastname@" ]
    [ "@R.DDSubCat.name@" "+Anthr":0 | "@D.DDSubCat.name@" ]
    [ "@R.DDSubCat.name+person@" "+Anthr":0 | "@D.DDSubCat.name+person@" ]
    [ "@R.DDSubCat.name+zoon@" "+Zoon":0 | "@D.DDSubCat.name+zoon@" ]
    [ "@R.DDSubCat.title@" "+Title":0 | "@D.DDSubCat.title@" ]
    [ "@R.DDSubCat.org@" ["+Org"]:0 | "@D.DDSubCat.org@" ]
    [ "@R.DDSubCat.geo+area@" ["+Top" "+Area"]:0 | "@D.DDSubCat.geo+area@" ]
    [ "@R.DDSubCat.geo+city@" ["+Top" "+Place"]:0 | "@D.DDSubCat.geo+city@" ]
    [ "@R.DDSubCat.geo+river@" ["+Top" "+Hydr"]:0 | "@D.DDSubCat.geo+river@" ]
    [ "@R.DDSubCat.geo+sea@" ["+Top" "+Hydr"]:0 | "@D.DDSubCat.geo+sea@" ]
    [ "@R.DDSubCat.geo+mount@" ["+Top" "+Area"]:0 | "@D.DDSubCat.geo+mount@" ]
    #[ "@R.DDSubCat.masdar@" ["+VN"]:0 | "@D.DDSubCat.masdar@" ]
    [ "@R.DDSubCat.past-part@" ["+Part" "+PastPart"]:0 | "@D.DDSubCat.past-part@" ]
    [ "@R.DDSubCat.present-part@" ["+Part" "+PresPart"]:0 | "@D.DDSubCat.present-part@" ]
    [ "@R.DDSubCat.future-part@" ["+Part" "+FutPart"]:0 | "@D.DDSubCat.future-part@" ]
    [ "@R.DDSubCat.negative-part@" ["+Part" "+NegPart"]:0 | "@D.DDSubCat.negative-part@" ]
    [ "@R.DDSubCat.mass@" ["+Mass"]:0 | "@D.DDSubCat.mass@" ]
    [ "@R.DDSubCat.animal@" ["+Anim"]:0 | "@D.DDSubCat.animal@" ]
    [ "@R.DDSubCat.temp@" ["+Temp"]:0 | "@D.DDSubCat.temp@" ]
    [ "@R.DDSubCat.anim@" ["+Hum"]:0 | "@D.DDSubCat.anim@" ]
    [ "@R.DDSubCat.anim+qual@" ["+Hum"]:0 | "@D.DDSubCat.anim+qual@" ]
 
    ]

  | "@D.DDCase@" "@R.DCase@"
  [ [ "@R.DCat.n@" "+N":0 | "@D.DCat.n@" ]
    [ "@R.DCat.a@" "+A":0 | "@D.DCat.a@" ]
    [ "@R.DCat.quant@" ["+A" "+Quant"]:0 | "@D.DCat.quant@" ]
    [ "@R.DCat.v-part@" "+VPart":0 | "@D.DCat.v-part@" ]
    [ "@R.DCat.masdar@" "+VN":0 | "@D.DCat.masdar@" ]
    [ "@R.DCat.prop@" "+N":0 "+Prop":0 | "@D.DCat.prop@" ]
    [ "@R.DCat.pron@" "+Pron":0 | "@D.DCat.pron@" ]
    [ "@R.DCat.allq@" ["+A" "+Quant"]:0 | "@D.DCat.allq@" ]
  
    [ "@R.DSubCat.anim+coll@" ["+Hum""+Coll"]:0 | "@D.DSubCat.anim+coll@" ]
    [ "@R.DSubCat.prop@" "+N":0 "+Prop":0 | "@D.DSubCat.prop@" ]
    [ "@R.DSubCat.elat@" "+Elat":0 | "@D.DSubCat.elat@" ]
    [ "@R.DSubCat.int@" "+Int":0 | "@D.DSubCat.int@" ]
    [ "@R.DSubCat.rel@" "+Rel":0 | "@D.DSubCat.rel@" ]
    [ "@R.DSubCat.poss@" "+Poss":0 | "@D.DSubCat.poss@" ]
    [ "@R.DSubCat.name+firstname@" ["+Anthr" "+FirstName"]:0 | "@D.DSubCat.name+firstname@" ]
    [ "@R.DSubCat.name+lastname@" ["+Anthr" "+LastName"]:0 | "@D.DSubCat.name+lastname@" ]
    [ "@R.DSubCat.name@" "+Anthr":0 | "@D.DSubCat.name@" ]
    [ "@R.DSubCat.name+person@" "+Anthr":0 | "@D.DSubCat.name+person@" ]
    [ "@R.DSubCat.title@" "+Title":0 | "@D.DSubCat.title@" ]
    [ "@R.DSubCat.orig@" ["+Org"]:0 | "@D.DSubCat.org@" ]
    [ "@R.DSubCat.geo+area@" ["+Top" "+Area"]:0 | "@D.DSubCat.geo+area@" ]
    [ "@R.DSubCat.geo+city@" ["+Top" "+Place"]:0 | "@D.DSubCat.geo+city@" ]
    [ "@R.DSubCat.geo+river@" ["+Top" "+Hydr"]:0 | "@D.DSubCat.geo+river@" ]
    [ "@R.DSubCat.geo+sea@" ["+Top" "+Hydr"]:0 | "@D.DSubCat.geo+sea@" ]
    [ "@R.DSubCat.geo+mount@" ["+Top" "+Area"]:0 | "@D.DSubCat.geo+mount@" ]
    #[ "@R.DSubCat.masdar@" ["+VN"]:0 | "@D.DSubCat.masdar@" ]
    [ "@R.DSubCat.past-part@" ["+Part" "+PastPart"]:0 | "@D.DSubCat.past-part@" ]
    [ "@R.DSubCat.present-part@" ["+Part" "+PresPart"]:0 | "@D.DSubCat.present-part@" ]
    [ "@R.DSubCat.future-part@" ["+Part" "+FutPart"]:0 | "@D.DSubCat.future-part@" ]
    [ "@R.DSubCat.negative-part@" ["+Part" "+NegPart"]:0 | "@D.DSubCat.negative-part@" ]
    [ "@R.DSubCat.mass@" ["+Mass"]:0 | "@D.DSubCat.mass@" ]
    [ "@R.DSubCat.animal@" ["+Anim"]:0 | "@D.DSubCat.animal@" ]
    [ "@R.DSubCat.temp@" ["+Temp"]:0 | "@D.DSubCat.temp@" ]
    [ "@R.DSubCat.anim@" ["+Hum"]:0 | "@D.DSubCat.anim@" ]
    [ "@R.DSubCat.anim+qual@" ["+Hum"]:0 | "@D.DSubCat.anim+qual@" ]
 
    ]

  | "@D.DDCase@" "@D.DCase@"
  [ [ "@R.Cat.n@" "+N":0 | "@D.Cat.n@" ]
    [ "@R.Cat.quant@" ["+A" "+Quant"]:0 | "@D.Cat.quant@" ]
    [ "@R.Cat.prop@" "+N":0 "+Prop":0 | "@D.Cat.prop@" ]
    [ "@R.Cat.v-part@" "+VPart":0 | "@D.Cat.v-part@" ]
    [ "@R.Cat.masdar@" "+VN":0 | "@D.Cat.masdar@" ]
    # [ "@R.Cat.num@" "+Num":0 | "@D.Cat.num@" ]
    [ "@R.Cat.a@" "@D.SubCat.int@" "+A":0 | "@R.Cat.a@" "@R.SubCat.int@" | "@D.Cat.a@" ]
    ]]

  [ "@R.Cat.pron@" "+Pron":0 | "@D.Cat.pron@" ]
  [ "@R.Cat.det@" "+Det":0 | "@D.Cat.det@" ]
  [ "@R.Cat.dem@" "+Pron":0 "+Dem":0 | "@D.Cat.dem@" ]
  [ "@R.Cat.pron+anim@" ["+Pron""+Hum"]:0 | "@D.Cat.pron+anim@" ]
  [ "@R.Cat.pron+inanim@" ["+Pron""+Nonhum"]:0 | "@D.Cat.pron+inanim@" ]
  [ "@R.Cat.pron+indef+anim@" ["+Pron" "+Indef" "+Hum"]:0 | "@D.Cat.pron+indef+anim@" ]
  [ "@R.Cat.pron+indef+inanim@" ["+Pron" "+Indef" "+Nonhum"]:0 | "@D.Cat.pron+indef+inanim@" ]
  [ "@R.Cat.pron+indef@" ["+Pron" "+Indef"]:0 | "@D.Cat.pron+indef@" ]
  [ "@R.Cat.pron+sindef@" ["+Pron" "+SIndef"]:0 | "@D.Cat.pron+sindef@" ]
  [ "@R.Cat.pron+sindef+anim@" ["+Pron" "+SIndef" "+Hum"]:0 | "@D.Cat.pron+sindef+anim@" ]
  [ "@R.Cat.pron+sindef+inanim@" ["+Pron" "+SIndef" "+Nonhum"]:0 | "@D.Cat.pron+sindef+inanim@" ]
  #[ "@R.Cat.det+anim@" ["+Pron" "+Dem" "+Hum"]:0 | "@D.Cat.det+anim@" ]
  #[ "@R.Cat.det+inanim@" ["+Pron" "+Dem" "+Nonhum"]:0 | "@D.Cat.det+inanim@" ]
  [ "@R.Cat.pron+interr+anim@" ["+Pron""+Int""+Hum"]:0 | "@D.Cat.pron+interr+anim@" ]
  [ "@R.Cat.pron+interr+inanim@" ["+Pron""+Int" "+Nonhum"]:0 | "@D.Cat.pron+interr+inanim@" ]
  [ "@R.Cat.pron+rel+anim@" ["+Pron""+Rel""+Hum"]:0 | "@D.Cat.pron+rel+anim@" ]
  [ "@R.Cat.pron+rel+inanim@" ["+Pron""+Rel" "+Nonhum"]:0 | "@D.Cat.pron+rel+inanim@" ]
  [ "@R.Cat.pron+neg+anim@" ["+Pron""+Neg""+Hum"]:0 | "@D.Cat.pron+neg+anim@" ]
  [ "@R.Cat.pron+neg+inanim@" ["+Pron""+Neg""+Nonhum"]:0 | "@D.Cat.pron+neg+inanim@" ]
  [ "@R.Cat.poss+interr+anim@" ["+Pron" "+Poss""+Int""+Hum"]:0 | "@D.Cat.poss+interr+anim@" ]
  [ "@R.Cat.poss+interr+inanim@" ["+Pron" "+Poss""+Int""+Nonhum"]:0 | "@D.Cat.poss+interr+inanim@" ]
  [ "@R.Cat.poss+neg+anim@" ["+Pron" "+Poss" "+Neg" "+Hum"]:0 | "@D.Cat.poss+neg+anim@" ]
  #  [ "@R.Cat.aint@" ["+AInt"]:0 | "@D.Cat.aint@" ]
  [ "@R.Cat.adv@" ["+Adv"]:0 | "@D.Cat.adv@" ]
  [ "@R.Cat.cj@" ["+Cj"]:0 | "@D.Cat.cj@" ]
  [ "@R.Cat.cj+sub@" ["+Cj" "+Sub"]:0 | "@D.Cat.cj+sub@" ]
  [ "@R.Cat.cj+coord@" ["+Cj" "+Coord"]:0 | "@D.Cat.cj+coord@" ]
  # [ "@R.Cat.dekl@" ["+Decl"]:0 | "@D.Cat.dekl@" ]
  [ "@R.Cat.int@" ["+Int"]:0 | "@D.Cat.int@" ]
  [ "@R.Cat.ad@" ["+Ad"]:0 | "@D.Cat.ad@" ]
  [ "@R.Cat.pf@" ["+Pf"]:0 | "@D.Cat.pf@" ] # ?? what are these again?
  [ "@R.Cat.pa@" ["+Pa"]:0 | "@D.Cat.pa@" ]
  [ "@R.Cat.pn@" ["+Pn"]:0 | "@D.Cat.pn@" ]
  [ "@R.Cat.pp@" ["+Pp"]:0 | "@D.Cat.pp@" ]
  [ "@R.Cat.ij@" ["+Interj"]:0 | "@D.Cat.ij@" ]
   # rename int to aint; keep track of those that contain Cat
  [ "@R.SubCat.int@" ["+Int"]:0 | "@D.SubCat.int@" ]
  [ "@R.Cat.q@" ["+A" "+Quant"]:0 | "@D.Cat.q@" ]
  [ "@R.Cat.allq@" ["+A" "+Quant"]:0 | "@D.Cat.allq@" ]
  [ "@R.Cat.neg@" ["+Adv" "+Neg"]:0 | "@D.Cat.neg@" ]
  #[ "@R.SubCat.masdar@" ["+VN"]:0 | "@D.SubCat.masdar@" ]
  [ "@R.SubCat.past-part@" ["+Part" "+PastPart"]:0 | "@D.SubCat.past-part@" ]
  [ "@R.SubCat.present-part@" ["+Part" "+PresPart"]:0 | "@D.SubCat.present-part@" ]
  [ "@R.SubCat.future-part@" ["+Part" "+FutPart"]:0 | "@D.SubCat.future-part@" ]
  [ "@R.SubCat.negative-part@" ["+Part" "+NegPart"]:0 | "@D.SubCat.negative-part@" ]
  [ "@R.SubCat.pers@" ["+Pers"]:0 | "@D.SubCat.pers@" ]
  [ "@R.SubCat.poss@" ["+Poss"]:0 | "@D.SubCat.poss@" ]
  [ "@R.Refl.+@" ["+Refl"]:0 | "@D.Refl.+@" ]
  [ "@R.SubCat.anim+prop?@" ["+Anim""+Prop?"]:0 | "@D.SubCat.anim+prop?@" ]
  [ "@R.SubCat.anim+coll@" ["+Anim""+Coll"]:0 | "@D.SubCat.anim+coll@" ]
  [ "@R.SubCat.rare@" ["+Rare"]:0 | "@D.SubCat.rare@" ]
  [ "@R.SubCat.prop@" ["+Prop"]:0 | "@D.SubCat.prop@" ] # where is that used??
  [ "@R.SubCat.elat@" ["+Elat"]:0 | "@D.SubCat.elat@" ]
  #[ "@R.SubCat.ord+digits@" ["+Ord" "+Digits"]:0 | "@D.SubCat.ord+digits@" ]
  [ "@R.SubCat.range@" "+Range":0 | "@D.SubCat.range@" ]
  [ "@R.SubCat.neg@" ["+Neg"]:0 | "@D.SubCat.neg@" ]
  [ "@R.SubCat.name+firstname@" ["+Anthr" "+FirstName"]:0 | "@D.SubCat.name+firstname@" ]
  [ "@R.SubCat.name+lastname@" ["+Anthr" "+LastName"]:0 | "@D.SubCat.name+lastname@" ]
  [ "@R.SubCat.name@" "+Anthr":0 | "@D.SubCat.name@" ]
  [ "@R.SubCat.name+person@" "+Anthr":0 | "@D.SubCat.name+person@" ]
  [ "@R.SubCat.title@" "+Title":0 | "@D.SubCat.title@" ]
  [ "@R.SubCat.org@" ["+Org"]:0 | "@D.SubCat.org@" ]
  [ "@R.SubCat.geo+area@" ["+Top" "+Area"]:0 | "@D.SubCat.geo+area@" ]
  [ "@R.SubCat.geo+city@" ["+Top" "+Place"]:0 | "@D.SubCat.geo+city@" ]
  [ "@R.SubCat.geo+river@" ["+Top" "+Hydr"]:0 | "@D.SubCat.geo+river@" ]
  [ "@R.SubCat.geo+sea@" ["+Top" "+Hydr"]:0 | "@D.SubCat.geo+sea@" ]
  [ "@R.SubCat.geo+mount@" ["+Top" "+Area"]:0 | "@D.SubCat.geo+mount@" ]
  [ "@R.SubCat.recip@" ["+Recip"]:0 | "@D.SubCat.recip@" ]
  [ "@R.SubCat.interr@" ["+Int"]:0 | "@D.SubCat.interr@" ]
  [ "@R.SubCat.rel@" ["+Rel"]:0 | "@D.SubCat.rel@" ]
  [ "@R.SubCat.indet@" ["+Indef"]:0 | "@D.SubCat.indet@" ]
  [ "@R.SubCat.indef-spec@" ["+SIndef"]:0 | "@D.SubCat.indef-spec@" ]
  [ "@R.SubCat.art@" ["+Art"]:0 | "@D.SubCat.art@" ]
  [ "@R.SubCat.num@" ["+Num"]:0 | "@D.SubCat.num@" ]
  [ "@R.SubCat.mass@" ["+Mass"]:0 | "@D.SubCat.mass@" ]
  # [ "@R.SubCat.meas@" ["+Meas"]:0 | "@D.SubCat.meas@" ]

  [ "@R.SubCat.meas@" "@D.CaseType.full@" "+Meas":0 | "@R.SubCat.meas@" "@R.CaseType.full@" | "@D.SubCat.meas@" ]

  [ "@R.SubCat.animal@" ["+Anim"]:0 | "@D.SubCat.animal@" ]
  [ "@R.SubCat.temp@" ["+Temp"]:0 | "@D.SubCat.temp@" ]
  [ "@R.SubCat.anim@" ["+Hum"]:0 | "@D.SubCat.anim@" ]
  [ "@R.SubCat.anim?@" ["+Hum"]:0 | "@D.SubCat.anim?@" ]

  [ "@R.SubCat.qual@" "@D.CaseType.full@" ["+Qual"]:0
  | "@R.SubCat.qual@" "@R.CaseType.full@"
  | "@D.SubCat.qual@" ]

  [ "@R.SubCat.anim+qual@" "@D.CaseType.full@" ["+Hum" "+Qual"]:0
  # e.g., ვარსკენ პიტიახში+Hum+Qual+Nom+Sg
  | "@R.SubCat.anim+qual@" "@R.CaseType.full@" ["+Hum":0 | ["+Hum" "+Qual" "+OG"]:0 ]
  | "@D.SubCat.anim+qual@" ]

  [ "@R.SubCat.anim?+qual@" "@D.CaseType.full@" ["+Hum" "+Qual"]:0
  | "@R.SubCat.anim?+qual@" "@R.CaseType.full@" ["+Hum":0 | ["+Hum" "+Qual" "+OG"]:0 ] | "@D.SubCat.anim?+qual@" ]

  [ "@R.SubCat.masd@" "+Masd":0 | "@D.SubCat.masd@" ]
 ]
 ;

# @R..@ : Require, @D..@ : Disallow, @U..@ : Unification test, @C.@ : Clear feature
# @P..@ : Positive (Re)Setting
define ResolveNounFlagDiacritics [

  [ "@R.DDCase@" #"@U.Lang.og@" # NG has no triple declension
  [
    [ "@R.DDCase.gen@" "+Gen":0 | "@D.DDCase.gen@" ] # only one disjunct is possible
    [ "@R.DDNum.sg@" [ "@D.DDCat.prop@" "+Sg":0 | "@R.DDCat.prop@" ] | "@D.DDNum.sg@" ]
    [ "@R.DDNum.pl@" "+Pl":0 | "@D.DDNum.pl@" ]
    [ "@R.DDOldPl.+@" "+OldPl":0 | "@D.DDOldPl.+@" "@R.DDNum.pl@" "+NewPl":0 | "@D.DDOldPl.+@" "@D.DDNum.pl@" ]
    # [ "@R.DDOldPl.+@" "+OldPl":0 | "@D.DDOldPl.+@" ] # only one disjunct is possible
    [ "@R.DDLong.+@" "+L":0 | "@D.DDLong.+@" ]
    
    [ "@R.DCase.gen@" "+DGen":0 ] #### | "@D.DCase.gen@" ]
    [ "@R.DNum.sg@" "+DSg":0 | "@D.DNum.sg@" ]
    [ "@R.DNum.pl@" "+DPl":0 | "@D.DNum.pl@" ]
  # [ "@R.DOldPl.+@" "+DOldPl":0 | "@D.DOldPl.+@" ]
    [ "@R.DOldPl.+@" "+DOldPl":0 | "@D.DOldPl.+@" "@R.DNum.pl@" "+DNewPl":0 | "@D.DOldPl.+@" "@D.DNum.pl@" ]
    [ "@R.DLong.+@" "+DL":0 | "@D.DLong.+@" ]

    [ "@R.Case.voc@" "+DDVoc":0 | "@D.Case.voc@" ]
    [ "@R.Case.adv@" "+DDAdvb":0 | "@D.Case.adv@" ]
    [ "@R.Case.adv-tr@" "+DDAdvb":0 "+DDTrunc":0 | "@D.Case.adv-tr@" ]
    [ "@R.Case.dat@" "+DDDat":0 | "@D.Case.dat@" ]
    [ "@R.Case.erg@" "+DDErg":0 | "@D.Case.erg@" ]
    [ "@R.Case.inst@" "+DDInst":0 | "@D.Case.inst@" ]
    # "@D.Case.gen@" "@D.Case.dir@" "@D.Case.ben@" # what about OG??
    [ "@R.Case.gen@" "+DDGen":0 | "@D.Case.gen@" ] # does DDGen exist?
    [ "@R.Case.dir@" "+DDDir":0 | "@D.Case.dir@" ]
    [ "@R.Case.ben@" "+DDBen":0 | "@D.Case.ben@" ]
    [ "@R.Case.nom@" "+DDNom":0 | "@D.Case.nom@" ]
    [ "@R.Num.sg@" "+DDSg":0 | "@D.Num.sg@" ]
    [ "@R.Num.pl@" "+DDPl":0 | "@D.Num.pl@" ]
  # [ "@R.OldPl.+@" "+DDOldPl":0 | "@D.OldPl.+@" ]
    [ "@R.OldPl.+@" "+DDOldPl":0 | "@D.OldPl.+@" "@R.Num.pl@" "+DDNewPl":0 | "@D.OldPl.+@" "@D.Num.pl@" ]
    [ "@R.Long.+@" "+DDL":0 | "@D.Long.+@" ]
    [ "@R.Style.dialect@" "+Dialect":0 | "@D.Style.dialect@" ]
    [ "@R.Style.old@" "+Old":0 | "@D.Style.old@" ]
    [ "@R.Style.nonstandard@" "+NonStand":0 | "@D.Style.nonstandard@" ]

    ]

  | "@D.DDCase@" "@R.DCase@"
  [
   # not yet [ "@R.DPv.+@" "+Pv":0 | "@D.DPv@" ] # for participles + VN
    [ "@R.DCase.gen@" "+Gen":0 | "@D.DCase.gen@" ]
    [ "@R.DNum.sg@" [ "@D.DCat.prop@" "+Sg":0 | "@R.DCat.prop@" ] | "@D.DNum.sg@" ]
    [ "@R.DNum.pl@" "+Pl":0 | "@D.DNum.pl@" ]
    [ "@R.DOldPl.+@" "+OldPl":0 | "@D.DOldPl.+@" "@R.DNum.pl@" "+NewPl":0 | "@D.DOldPl.+@" "@D.DNum.pl@" ]
  # [ "@R.DOldPl.+@" "+OldPl":0 | "@D.DOldPl.+@" ] 
  # [ "@R.DerType.იანი@" "+Der:იანი":0 | "@D.DerType.იანი@" ]

    [ "@R.Case.voc@" "+DVoc":0 | "@D.Case.voc@" ]
    [ "@R.Case.adv-tr@" "+DAdvb":0 "+DTrunc":0 | "@D.Case.adv-tr@" ]
    [ "@R.Case.adv@" "+DAdvb":0  | "@D.Case.adv@" ]
    [ "@R.Case.dat@" "+DDat":0 | "@D.Case.dat@" ]
    [ "@R.Case.erg@" "+DErg":0 | "@D.Case.erg@" ]
    [ "@R.Case.inst@" "+DInst":0 | "@D.Case.inst@" ]
    # "@D.Case.gen@" "@D.Case.dir@" "@D.Case.ben@" # what about OG??
    [ "@R.Case.gen@" "+DGen":0 | "@D.Case.gen@" ] # allowed for NG??
    [ "@R.Case.dir@" "@U.Lang.og@" "+DDir":0 | "@D.Case.dir@" ]
    [ "@R.Case.ben@" "@U.Lang.og@" "+DBen":0 | "@D.Case.ben@" ]
    [ "@R.Case.nom@" "+DNom":0 | "@D.Case.nom@" ]
    [ "@R.Num.sg@" "+DSg":0 | "@D.Num.sg@" ]
    [ "@R.Num.pl@" "+DPl":0 | "@D.Num.pl@" ]
  # [ "@R.OldPl.+@" "+DOldPl":0 | "@D.OldPl.+@" ]
    [ "@R.OldPl.+@" "+DOldPl":0 | "@D.OldPl.+@" "@R.Num.pl@" "+DNewPl":0 | "@D.OldPl.+@" "@D.Num.pl@" ]
    [ "@R.Long.+@" "+DL":0 | "@D.Long.+@" ]
    [ "@R.Style.dialect@" "+Dialect":0 | "@D.Style.dialect@" ]
    [ "@R.Style.old@" "+Old":0 | "@D.Style.old@" ]
    [ "@R.Style.nonstandard@" "+NonStand":0 | "@D.Style.nonstandard@" ]    
    ]

  | "@D.DDCase@" "@D.DCase@"
  [ [ "@R.Pv.+@" "+Pv":0 | "@D.Pv@" ] # for participles + VN
    [ "@R.NaPastPart.+@" "+PastPart:ნა":0 | "@D.NaPastPart@" ]
    [ "@R.Pers.3@" "+3":0 | "@D.Pers.3@" ]
    [ "@R.Pers.2@" "+2":0 | "@D.Pers.2@" ]
    [ "@R.Pers.1@" "+1":0 | "@D.Pers.1@" ]
    [ "@R.PossPers.3sg@" "+Poss3Sg":0 | "@D.PossPers.3sg@" ]
    [ "@R.PossPers.2sg@" "+Poss2Sg":0 | "@D.PossPers.2sg@" ]
    [ "@R.PossPers.1sg@" "+Poss1Sg":0 | "@D.PossPers.1sg@" ]
    [ "@R.PossPers.3pl@" "+Poss3Pl":0 | "@D.PossPers.3pl@" ]
    [ "@R.PossPers.2pl@" "+Poss2Pl":0 | "@D.PossPers.2pl@" ]
    [ "@R.PossPers.1pl@" "+Poss1Pl":0 | "@D.PossPers.1pl@" ]
    [ "@R.Deixis.prox@" "+Prox":0 | "@R.Deixis.med@" "+Med":0 | "@R.Deixis.dist@" "+Dist":0 | "@D.Deixis@" ]
    [ "@R.Case.voc@" "+Voc":0 | "@D.Case.voc@" ]
    [ "@R.Case.adv@" "+Advb":0 | "@D.Case.adv@" ] # have to use +Advb for the case to avoid confusion with adverbs in CG
    [ "@R.Case.adv-tr@" "+Advb":0 "+Trunc":0 | "@D.Case.adv-tr@" ] 
    [ "@R.Case.dat@" "+Dat":0 | "@D.Case.dat@" ]
    [ "@R.Case.erg@" "+Erg":0 | "@D.Case.erg@" ]
    [ "@R.Case.inst@" "+Inst":0 | "@D.Case.inst@" ]
    [ "@R.Case.gen@" "+Gen":0 | "@D.Case.gen@" ]
    [ "@R.Case.nom@" "+Nom":0 | "@D.Case.nom@" ]
    [ "@R.Case.abs@" "+Abs":0 | "@D.Case.abs@" ]
    [ "@R.Case.dir@" "+Dir":0 | "@D.Case.dir@" ]
    [ "@R.Case.ben@" "+Ben":0 | "@D.Case.ben@" ]
    # Proper nouns are not marked for Sg
    [ "@R.Num.sg@" [ "@D.Cat.prop@" "+Sg":0 | "@R.Cat.prop@" ] | "@D.Num.sg@" ]
    [ "@R.Num.pl@" "+Pl":0 | "@D.Num.pl@" ]
    # [ "@R.OldPl.+@" "+OldPl":0 | "@D.OldPl.+@" "@R.Num.pl@" "+NewPl":0 | "@D.OldPl.+@" "@D.Num.pl@" ]
    [ "@R.OldPl.+@" "+OldPl":0 | "@R.OldPl.-@" "+NewPl":0 | "@D.OldPl@" ]
    [ "@R.DoubleOldPl.+@" "+OldPl":0 | "@D.DoubleOldPl@" ]
    [ "@R.Long.+@" "+L":0 | "@D.Long.+@" ]
    [ "@R.Style.dialect@" ["+Dialect"]:0 | "@D.Style.dialect@" ]
    [ "@R.Style.old@" ["+Old"]:0 | "@D.Style.old@" ]
    [ "@R.Style.nonstandard@" ["+NonStand"]:0 | "@D.Style.nonstandard@" ]
    ]]

  # [ "@R.DerType.იანი@" "+Der:იანი":0 | "@D.DerType.იანი@" ]

  [ "@R.Form.bound@" "+Att":0 | "@D.Form.bound@" ]
  # [ "@R.DetForm.bound@" "+Att":0 | "@D.DetForm.bound@" ]
  [ "@R.CaseType.reduced@" "+Att":0 | "@D.CaseType.reduced@" ]
  [ "@R.CaseType.reduced-ext@" "+Att":0 "+Ext":0 | "@D.CaseType.reduced-ext@" ] # extended Inst (-ის)
  [ "@R.CaseType.reduced-oldpl@" "+Att":0 "+<OldPl>":0 | "@D.CaseType.reduced-oldpl@" ]
  # [ "@R.CaseType.full@" "+Full":0 | "@D.CaseType.full@" ] # not reduced is full
  [ "@R.Pp.დმი@" "+PP":0 "+PP:დმი":0 | "@D.Pp.დმი@" ]
  [ "@R.Pp.დამი@" "+PP":0 "+PP:დამი":0 | "@D.Pp.დამი@" ]
  [ "@R.Pp.მი@" "+PP":0 "+PP:მი":0 | "@D.Pp.მი@" ] # OG -დამი, -დამო
  [ "@R.Pp.მო@" "+PP":0 "+PP:მო":0 | "@D.Pp.მო@" ]
  [ "@R.Pp.და@" "+PP":0 "+PP:და":0 | "@D.Pp.და@" ]
  [ "@R.Pp.ებრივ@" "+PP":0 "+PP:ებრივ":0 | "@D.Pp.ებრივ@" ]
  [ "@R.Pp.ებრ@" "+PP":0 "+PP:ებრ":0 | "@D.Pp.ებრ@" ]
  [ "@R.Pp.მებრ@" "+PP":0 "+PP:მებრ":0 | "@D.Pp.მებრ@" ]
  [ "@R.Pp.კენ@" "+PP":0 "+PP:კენ":0 | "@D.Pp.კენ@" ]
  [ "@R.Pp.კე@" "+PP":0 "+PP:კე":0 | "@D.Pp.კე@" ] # რუსთაველი
  [ "@R.Pp.თვის@" "+PP":0 "+PP:თვის":0 | "@D.Pp.თვის@" ]
  # [ "@R.Pp.თვინა@" "+PP":0 "+PP:თვის":0  | "@D.Pp.თვინა@" ]
  [ "@R.Pp.გან@" "+PP":0 "+PP:გან":0 | "@D.Pp.გან@" ]
  [ "@R.Pp.დან@" "+PP":0 "+PP:დან":0 | "@D.Pp.დან@" ]
  [ "@R.Pp.დამ@" "+PP":0 "+PP:დამ":0 "+Old":0 | "@D.Pp.დამ@" ]
  [ "@R.Pp.ურთ@" "+PP":0 "+PP:ურთ":0 | "@D.Pp.ურთ@" ]
  [ "@R.Pp.მდე@" "+PP":0 "+PP:მდე":0 | "@D.Pp.მდე@" ]
  [ "@R.Pp.მდის@" "+PP":0 "+PP:მდის":0 | "@D.Pp.მდის@" ]
  [ "@R.Pp.მდინ@" "+PP":0 "+PP:მდინ":0 "+Old":0 | "@D.Pp.მდინ@" ]
  [ "@R.Pp.მდი@" "+PP":0 "+PP:მდი":0 "+Old":0 | "@D.Pp.მდი@" ]
  [ "@R.Pp.ქვეშ@" "+PP":0 "+PP:ქვეშ":0 | "@D.Pp.ქვეშ@" ]
  [ "@R.Pp.წინ@" "+PP":0 "+PP:წინ":0 | "@D.Pp.წინ@" ]
  [ "@R.Pp.ზე@" "+PP":0 "+PP:ზე":0 | "@D.Pp.ზე@" ]
  [ "@R.Pp.ზედ@" "+PP":0 "+PP:ზედ":0 | "@D.Pp.ზედ@" ]
  [ "@R.Pp.შუა@" "+PP":0 "+PP:შუა":0 | "@D.Pp.შუა@" ]
  [ "@R.Pp.ში@" "+PP":0 "+PP:ში":0 | "@D.Pp.ში@" ]
  [ "@R.Pp.თან@" "+PP":0 "+PP:თან":0 | "@D.Pp.თან@" ]
  [ "@R.Pp.ვით@" "+PP":0 "+PP:ვით":0 | "@D.Pp.ვით@" ]
  [  "@R.Comp-Case.dat@" "+<Dat>":0 # Complement case
   | "@R.Comp-Case.gen@" "+<Gen>":0
   | "@R.Comp-Case.dat-gen@" "+<Dat/Gen>":0
   | "@R.Comp-Case.inst@" "+<Inst>":0
   | "@R.Comp-Case.adv@" "+<Advb>":0
   | "@D.Comp-Case@" ]
  [   "@R.Comp-Pp.თან@" "+<PP:თან>":0
   |  "@R.Comp-Pp.გან@" "+<PP:გან>":0
   |  "@D.Comp-Pp@" ]
  [ "@R.NegType.pot@" "+Pot":0 | "@D.NegType.pot@" ]
  [ "@R.NegType.imp@" "+Imp":0 | "@D.NegType.imp@" ]
  [ "@R.AdvType.loc@" "+Loc":0 | "@D.AdvType.loc@" ]
  [ "@R.AdvType.temp@" "+Temp":0 | "@D.AdvType.temp@" ]
  [ "@R.AdvType.mann@" "+Mann":0 | "@D.AdvType.mann@" ]
  [ "@R.AdvType.causal@" "+Causal":0 | "@D.AdvType.causal@" ]
  [ "@R.AdvType.final@" "+Final":0 | "@D.AdvType.final@" ]
  [ "@R.ModSfx.ღა@" "+Encl:ღა":0 "+Foc":0 | "@D.ModSfx.ღა@" ]
  [ "@R.ModSfx.ვე@" "+Encl:ვე":0 "+Foc":0 | "@D.ModSfx.ვე@" ]
  [ "@R.ModSfx.მც@" "+Encl:მც":0 "+Foc":0 | "@D.ModSfx.მც@" ]
  [ "@R.Approx.+@" "+Approx":0 | "@D.Approx.+@" ]
  [ "@R.Repet.+@" "+Repet":0 | "@D.Repet.+@" ]
  # Encl/Rel is disambiguated in CG
  [ "@R.RelSfx.ც@" [ "+Encl:ც":0 "+Foc":0 | "+Rel:ც":0 ] | "@D.RelSfx.ც@" ]
  [ "@R.RelSfx.ცა@" "+Encl:ც":0 "+Foc":0 | "@D.RelSfx.ცა@" ]
  [ "@R.RelSfx.ესე@" "+RelProx":0 | "@D.RelSfx.ესე@" ]
  [ "@R.RelSfx.ეგე@" "+RelMed":0 | "@D.RelSfx.ეგე@" ]
  [ "@R.RelSfx.იგი@" "+RelDist":0 | "@D.RelSfx.იგი@" ]
  [ "@R.RelSfx.ცა-ესე@" "+Encl:ც":0 "+RelProx":0 | "@D.RelSfx.ცა-ესე@" ]
  [ "@R.RelSfx.ცა-ეგე@" "+Encl:ც":0 "+RelMed":0 | "@D.RelSfx.ცა-ეგე@" ]
  [ "@R.RelSfx.ცა-იგი@" "+Encl:ც":0 "+RelDist":0 | "@D.RelSfx.ცა-იგი@" ]
  [ "@R.Deriv.ურ@" "+<Der:ურ>":0
   | "@R.Deriv.ელ@" "+<Der:ელ>":0
   | "@R.Deriv.მაგვარ@" "+<Der:მაგვარ>":0
   | "@R.Deriv.ებრ@" "+<Der:ებრ>":0
   | "@R.Deriv.ეულ@" "+<Der:ეულ>":0
   | "@R.Deriv.ფერ@" "+<Der:ფერ>":0
   | "@R.Deriv.ნაირ@" "+<Der:ნაირ>":0
   | "@R.Deriv.ობრივ@" "+<Der:ობრივ>":0
   | "@R.Deriv.იან@" "+<Der:იან>":0
   | "@R.Deriv.სა-ო@" "+<Der:სა-ო>":0
   | "@R.Deriv.უ-ო@" "+<Der:უ-ო>":0
   | "@D.Deriv@" ]
  [ "@R.Lang.ng@" "+NG":0 | "@D.Lang.ng@" ]
  [ "@R.Lang.mg@" "+MG":0 | "@D.Lang.mg@" ]
  [ "@R.Lang.og@" "+OG":0 | "@D.Lang.og@" ]
  #  [ "@R.EnclQopna.+@" "+Encl:Aux":0 | "@D.EnclQopna.+@" ]
 ] ;

define AugmentCardFlagDiacritics "+Card":0 "@C.StemType@" "@C.Digvalue@" # "@C.Lex@"
        "@C.ConjNum@";
define AugmentConjNumFlagDiacritics "+ConjNum":0 "@C.StemType@" "@C.Digvalue@" # "@C.Lex@"
        "@C.ConjNum@";

# don't change this line:
# <--- automatically generated code will be inserted here --->

save defined noun.vars

define RFDNoun Noun
       	       ResolveCatFlagDiacritics ResolveNounFlagDiacritics
               [ "@R.EnclQopna.+@" "+Encl:Aux":0 | "@D.EnclQopna.+@" ];

read regex [ 0 <- "+Sg" || "+Refl" ?* _ ] .o. RFDNoun ;

echo > eliminate flag Cat
eliminate flag Cat
echo > eliminate flag SubCat
eliminate flag SubCat
echo > eliminate flag Pv
eliminate flag Pv
echo > eliminate flag NaPastPart
eliminate flag NaPastPart

  echo > eliminate flag DCat
  eliminate flag DCat
  echo > eliminate flag DDCat
  eliminate flag DDCat
  echo > eliminate flag DCase
  eliminate flag DCase
  echo > eliminate flag StemType
  eliminate flag StemType
echo > eliminate flag Refl
eliminate flag Refl


  echo > eliminate flag Form
  eliminate flag Form
#  echo > eliminate flag DetForm
#  eliminate flag DetForm
  echo > eliminate flag Lang
  eliminate flag Lang


 echo > eliminate flag Morph
 eliminate flag Morph
 echo > eliminate flag Comp-Pp
 eliminate flag Comp-Pp
# echo > eliminate flag Lex
# eliminate flag Lex
 echo > eliminate flag Comp-Case
 eliminate flag Comp-Case

  echo > eliminate flag Pers
  eliminate flag Pers
  echo > eliminate flag PossPers
  eliminate flag PossPers
  echo > eliminate flag RigidStem
  eliminate flag RigidStem
 # echo > eliminate flag Pp
 # eliminate flag Pp
  echo > eliminate flag Case
  eliminate flag Case
  echo > eliminate flag CaseType
  eliminate flag CaseType
  echo > eliminate flag Num
  eliminate flag Num
  echo > eliminate flag DNum
  eliminate flag DNum
  echo > eliminate flag DDNum
  eliminate flag DDNum
  echo > eliminate flag Double
  eliminate flag Double
  echo > eliminate flag OldPl
  eliminate flag OldPl
  echo > eliminate flag DOldPl
  eliminate flag DOldPl
  echo > eliminate flag DDOldPl
  eliminate flag DDOldPl
  echo > eliminate flag DoubleOldPl
  eliminate flag DoubleOldPl
  echo > eliminate flag DerType
  eliminate flag DerType
  echo > eliminate flag PlStem
  eliminate flag PlStem
  echo > eliminate flag SyncStem
  eliminate flag SyncStem
echo > eliminate flag DDCase
eliminate flag DDCase
  echo > eliminate flag Long
  eliminate flag Long
  echo > eliminate flag DLong
  eliminate flag DLong
  echo > eliminate flag DDLong
  eliminate flag DDLong
echo > eliminate flag DSubCat
eliminate flag DSubCat
echo > eliminate flag DDSubCat
eliminate flag DDSubCat
  echo > eliminate flag RelSfx
  eliminate flag RelSfx
  echo > eliminate flag ModSfx
  eliminate flag ModSfx
  echo > eliminate flag Approx
  eliminate flag Approx
  echo > eliminate flag Repet
  eliminate flag Repet
  echo > eliminate flag Deixis
  eliminate flag Deixis
#  echo > eliminate flag DDL
#  eliminate flag DDL

# endif

echo > eliminate flag EnclQopna
eliminate flag EnclQopna

echo > eliminate flag NegType
eliminate flag NegType

echo > eliminate flag AdvType
eliminate flag AdvType

echo > eliminate flag Style
eliminate flag Style
echo > eliminate flag Deriv
eliminate flag Deriv


define Noun ;

#define Card [?* "+Num" "+Card" ?* ] .o. Noun ;

#define CardNom [?* [ "+Nom" | "+Gen" | "+Inst"] ?* ] .o. Card ;
#define CardDat [?* [ "+Dat" | "+Advb" ] ?* ] .o. Card ;
#define CardErg [?* "+Erg" ?* ] .o. Card ;

#define CardRedNom [ [?* "+Num" "+Card" "+Alpha" "+Nom" "+Att" ?* ] .o. CardNom .o. ["-" -> 0, "–" -> 0 ]].l ;

#define CardRedDat [ CardRedNom .o. [ ი -> 0 || _ .#. ] ] ;
#define CardRedErg [ CardRedNom .o. [ ი -> {მა} || _ .#. ] ] ;

# is empty!
#define Range [ CardRedNom "-" CardNom
#       	     | CardRedDat "-" CardDat
#       	     | CardRedErg "-" CardErg
#	     ] ;

read regex      Noun # [ Noun | Range ]
     	    .o. [ {აd} (->) დ || [ლ|მ|ნ|რ] _ ] #  იერუსალჱმდ
	    .o. [ {ილაd} (->) {იდ} ] # სიკუდიდ(მდე)
	    .o. [ d -> დ ]
	    .o. [ V -> ვ ]
	    ;

echo save stack noun.fst
save stack noun.fst

# EOF