load stack redup-ng.fst
define Morph ;
define PvVerb [?* ["+Act"|"+Pass"|"+MedAct"|"+MedPass"] ?*] .o. [?* "+Pv" ?*] .o. Morph ;
define NoPvVerb [?* ["+Act"|"+Pass"|"+MedAct"|"+MedPass"] ?*] .o. \"+Pv"+ .o. Morph ;

define lemmaGenusChar  ა|ბ|გ|დ|ე|ვ|ზ|თ|ი|კ|ლ|მ|ნ|ო|პ|ჟ|რ|ს|ტ|უ|ფ|ქ|ღ|ყ|შ|ჩ|ც|ძ|წ|ჭ|ხ|ჯ|ჰ|ჴ|ჱ|ჲ|ჵ|ჳ
       		 |"["|"]"|"/"|"-"|"{"|"}"
		 | "+Act"| "+Pass" | "+MedAct" | "+MedPass" ;

define PvRedup [ 0 <- "["|"]" || _ ?* "/" ]
       	   .o. [ 0 <- \lemmaGenusChar]
	   .o. [?* "+Redup" ?*]
	   .o. [?* "+Pv" ?*]
	   .o. Morph ;

define NoPvRedup [ 0 <- "["|"]" || _ ?* "/" ]
       	   .o. [ 0 <- \lemmaGenusChar]
	   .o. [?* "+Redup" ?*]
	   .o. \"+Pv"+
	   .o. Morph ;

define PvRedupAct [ 0 <- "+Act"] .o. [?* "+Act" ?*] .o. PvRedup ;
define PvRedupPass [ 0 <- "+Pass"] .o. [?* "+Pass" ?*] .o. PvRedup ;
define PvRedupMedAct [ 0 <- "+MedAct"] .o. [?* "+MedAct" ?*] .o. PvRedup ;
define PvRedupMedPass [ 0 <- "+MedPass"] .o. [?* "+MedPass" ?*] .o. PvRedup ;
define NoPvRedupAct [ 0 <- "+Act"] .o. [?* "+Act" ?*] .o. NoPvRedup ;
define NoPvRedupPass [ 0 <- "+Pass"] .o. [?* "+Pass" ?*] .o. NoPvRedup ;
define NoPvRedupMedAct [ 0 <- "+MedAct"] .o. [?* "+MedAct" ?*] .o. NoPvRedup ;
define NoPvRedupMedPass [ 0 <- "+MedPass"] .o. [?* "+MedPass" ?*] .o. NoPvRedup ;
define PvVerbAct [?* "+Act" ?*] .o. PvVerb ;
define PvVerbPass [?* "+Pass" ?*] .o. PvVerb ;
define PvVerbMedAct [?* "+MedAct" ?*] .o. PvVerb ;
define PvVerbMedPass [?* "+MedPass" ?*] .o. PvVerb ;
define NoPvVerbAct [?* "+Act" ?*] .o. NoPvVerb ;
define NoPvVerbPass [?* "+Pass" ?*] .o. NoPvVerb ;
define NoPvVerbMedAct [?* "+MedAct" ?*] .o. NoPvVerb ;
define NoPvVerbMedPass [?* "+MedPass" ?*] .o. NoPvVerb ;

define PvRedupVerb [ PvRedupAct "=":0 PvVerbAct
       		 | PvRedupPass "=":0 PvVerbPass
		 | PvRedupMedAct "=":0 PvVerbMedAct
		 | PvRedupMedPass "=":0 PvVerbMedPass ] ;

define NoPvRedupVerb [ NoPvRedupAct "=":0 NoPvVerbAct
       		 | NoPvRedupPass "=":0 NoPvVerbPass
		 | NoPvRedupMedAct "=":0 NoPvVerbMedAct
		 | NoPvRedupMedPass "=":0 NoPvVerbMedPass ] ;

define PvRedupVerb [ "+Pv" "+Redup" <- "+Pv" ] .o. PvRedupVerb ;
define NoPvRedupVerb [ ... "+Redup" <- ["+Act"|"+Pass"|"+MedAct"|"+MedPass"] ] .o. NoPvRedupVerb ;

read regex PvRedupVerb | NoPvRedupVerb ;

save stack georgian-redup-ng.fst

# გა-სვლ[ა]/ვ{ი}დ=გამო-სვლ[ა]/ვ{ი}დ+V+MedPass+Redup+Aor+Pv+<S>+<S:Nom>+S:3Sg
# =>
# გა-სვლა=გამო-სვლ[ა]/ვ{ი}დ+V+MedPass+Redup+Aor+Pv+<S>+<S:Nom>+S:3Sg

# გატეხ-გახვრიტა
# გა-ტეხ[ა]/ტეხ=გა-ხვრეტ[ა]/ხვრიტ+V+Act+Redup+Aor+Pv+<S-DO>+S:3Sg+DO:3
# =>
# გა-ტეხა=გა-ხვრეტ[ა]/ტეხ=ხვრიტ+V+Act+Redup+Aor+Pv+<S-DO>+S:3Sg+DO:3


# missing:

# გაჰკრავ-გამოჰკრავს