# $ stands after the prefix person marker
# now used for anything except verbs

# should be included in the general verb morphology, which isn't trivial
define OGExceptions [ {ყოფ[ა]/ყ[ავ]}:{იყავნ}
       		      ["+V" "+Pass" "+IterPres" "+<S>" "+<AuxIntr>" "+<S:Nom>" "+S:3Sg"]:0
		    | [ {სახლ·ი}:[{სახედ}|{სახიდ}] ["+N" "+Advb" "+Sg"]:0
		      ] OGNounEnclitics
		    ] ;

define OldGeorgianUpper  [ "+A" "+Part" <- "+Part" ] # todo: move elsewhere
	 	     .o. [ "+N" "+VN" <- "+VN" ]
		     .o. [ 0 <- "+FirstName" ]
		     .o. simplifyFrame
 		     .o. normalizeOGTags
		     .o. OGCommon
		     .o. OldG
		     ;

define OldGeorgian  [    OldGeorgianUpper
		     .o. oldGeoOrth ]
		    | OGExceptions
		    | OGTmesisInfix # treated as separate words; tmesis splitting is done programmatically
		    | CardinalDigits ( (0:"-") cardPP )
		    | CardinalDigitsRange ( (0:"-") cardPP )
		    | OGLetterNum
		    | RomanNum ;

   read regex \"+Redup"+ .o. OldGeorgian .o. [ "#" -> 0 || _ ? ] ;
   echo > eliminate flag Frame
   eliminate flag Frame
   echo > eliminate flag Class
   eliminate flag Class
 #  echo > eliminate flag StemType
 #  eliminate flag StemType

define OldGeorgian

# see entries with PV = § in verb lexicon
define OGNonComp \"§"+ .o. OldGeorgian ;

define OGCompSfx     [ 0 <- "§" ]
       		 .o. [ ... "+VComp" <- ["+V"|"+VN"|"+PresPart"|"+PastPart"|"+FutPart"|"+NegPart"] ]
       		 .o. [ "§" ?+ ]
		 .o. \"+<AuxIntr>"+
		 .o. OldGeorgian
		 .o. [ "§" -> 0 ] ;

define OldGeorgian OGNonComp | georgianChar+ 0:"-" OGCompSfx ;

read regex OldGeorgian ;

save stack georgian-morph-og.fst
pop stack

# TODO: add compound verbs to Xanmeti, Haemeti

#   read regex OldGeorgianUpper ;
#   echo > eliminate flag Frame
#   eliminate flag Frame
#   save stack georgian-morph-og-upper.fst
#   pop stack

   define OGXanmeti [    [ "+A" "+Part" <- "+Part" ] # todo: move elsewhere
		     .o. [ "+N" "+VN" <- "+VN" ]
		     .o. [ 0 <- "+FirstName" ]
		     .o. simplifyFrame
		     .o. normalizeOGTags
		     .o. OGCommon
		     .o. Xanmeti
		     .o. oldGeoOrth ]
		    | OGExceptions
		    | OGTmesisInfix
		    | CardinalDigits ( (0:"-") cardPP )
		    | CardinalDigitsRange ( (0:"-") cardPP )
		    | OGLetterNum
		    | RomanNum ;
   read regex \"+Redup"+ .o. OGXanmeti .o. [ "#" -> 0 || _ ? ] ;
   echo > eliminate flag Frame
   eliminate flag Frame
   save stack georgian-morph-xanmeti.fst
   pop stack

   undefine OGXanmeti ;

   define OGHaemeti [     [ "+A" "+Part" <- "+Part" ] # todo: move elsewhere
		      .o. [ "+N" "+VN" <- "+VN" ]
		      .o. [ 0 <- "+FirstName" ]
		      .o. simplifyFrame
		      .o. normalizeOGTags
		      .o. OGCommon
		      .o. Haemeti
		      .o. oldGeoOrth ]
		    | OGExceptions
		    | OGTmesisInfix
		    | CardinalDigits ( (0:"-") cardPP )
		    | CardinalDigitsRange ( (0:"-") cardPP )
		    | OGLetterNum
		    | RomanNum ;

   read regex \"+Redup"+ .o. OGHaemeti .o. [ "#" -> 0 || _ ? ] ;
   echo > eliminate flag Frame
   eliminate flag Frame
   save stack georgian-morph-haemeti.fst
   pop stack

   undefine OGHaemeti ;
   undefine OGCommon ;

# EOF
